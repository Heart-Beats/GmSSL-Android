apply plugin: 'maven-publish'

def GITHUB_REPOSITORY = "https://maven.pkg.github.com/Heart-Beats/GmSSL-Android/"
def GITHUB_USER = "Heart-Beats"
def GITHUB_PERSONAL_ACCESS_TOKEN = GITHUB_PACKAGES_WRITE_TOKEN

def isPublishRelease = true

afterEvaluate {
    publishing {
        repositories {
            maven {
                allowInsecureProtocol(true)
                name "GitHubPackages"
                url GITHUB_REPOSITORY
                credentials {
                    username = GITHUB_USER
                    password = GITHUB_PERSONAL_ACCESS_TOKEN
                }
            }
        }

        publications {
            Release(MavenPublication) {
                from components.release
                groupId = 'com.hl.gmssl'
                artifactId = 'android'
                def VERSION_SUFFIX = isPublishRelease.toBoolean() ? '' : '-SNAPSHOT'
                version = '3.1.0' + VERSION_SUFFIX
            }
        }
    }
}

// type显示指定任务类型或任务, 这里指定要执行Javadoc这个task,这个task在gradle中已经定义
task androidJavadocs(type: Javadoc) {
    // 设置源码所在的位置
    source = android.sourceSets.main.java.sourceFiles
}

// 生成javadoc.jar， 指定文档名称
task androidJavadocsJar(type: Jar) {
    // Gradle 8.0 之后 classifier 属性被移除 改用 archiveClassifier
    archiveClassifier = 'javadoc'
    from androidJavadocs.destinationDir
}

// 打包main目录下代码和资源的task，生成sources.jar
task androidSourcesJar(type: Jar) {
    // Gradle 8.0 之后 classifier 属性被移除 改用 archiveClassifier
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

//配置需要上传到maven仓库的文件
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}


tasks.whenTaskAdded { task ->
    if (task.name.startsWith("publish")) {
        // 打包发布之前清除之前的编译产物
        Task cleanTask = project.getTasksByName('clean', false).first()
        task.dependsOn(cleanTask)
        task.mustRunAfter(cleanTask)
    }
}